import java.time.LocalDateTime

allprojects {
    group APP_GROUP
    version APP_VERSION
}

description = "Provide springboot starter for dify to simplify development."

subprojects {
    apply plugin: 'java-library'
    apply plugin: 'jacoco'
    apply plugin: 'signing'
    apply plugin: 'maven-publish'
    apply plugin: 'tech.yanand.maven-central-publish'
    apply plugin: 'io.freefair.lombok'
    apply plugin: 'com.github.hierynomus.license'
    apply plugin: 'io.spring.dependency-management'

    // 使用 Java 8 以保持向后兼容性
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8

    ext {
        isBomProject = project.name == "dify-bom"
        springBootVersionForProject = getSpringBootVersionForProject(project)
    }

    lombok {
        version = libs.versions.lombok.get()
    }

    compileJava {
        options.release = 8
    }

    compileTestJava {
        options.release = 21
    }

    repositories {
        mavenLocal()
        mavenCentral()
    }

    // 仅非BOM项目应用依赖管理
    if (!isBomProject) {
        configureDependencyManagement(project)
    }

    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
        options.warnings = false
        options.deprecation = true
        options.compilerArgs += ["-Xlint:unchecked", "-parameters"]
    }

    tasks.withType(GenerateModuleMetadata) {
        enabled = false
    }

    jar {
        into("META-INF/") {
            from rootProject.file("LICENSE")
        }
        into("META-INF/maven/$project.group/$project.name") {
            from { generatePomFileForMavenJavaPublication }
            rename ".*", "pom.xml"
        }
        afterEvaluate {
            manifest {
                attributes 'Implementation-Title': archiveBaseName
                attributes 'Implementation-Version': archiveVersion
                attributes 'Built-Gradle': gradle.gradleVersion
                attributes 'Bundle-DocURL': 'https://guoshiqiufeng.github.io/dify-spring-boot-starter'
                attributes 'Build-OS': System.getProperty("os.name")
                attributes 'Built-By': System.getProperty("user.name")
                attributes 'Build-Jdk': System.getProperty("java.version")
                attributes 'Build-Timestamp': LocalDateTime.now().format("yyyy-MM-dd HH:mm:ss")
                attributes 'Automatic-Module-Name': "${project.group}.${project.name.replaceAll("-", ".")}"
            }
        }
    }

    license {
        encoding = "UTF-8"
        header = rootProject.file("header.txt")
        includes(["**/*.java", "**/*.kt"])
        mapping {
            java = "SLASHSTAR_STYLE"
            kt = "SLASHSTAR_STYLE"
        }
        ignoreFailures = true
        ext.year = Calendar.getInstance().get(Calendar.YEAR)
    }

    task sourcesJar(type: Jar) {
        archiveClassifier = 'sources'
        from sourceSets.main.allSource
    }

    javadoc {
        // Check for syntax during linting. 'none' doesn't seem to work in suppressing
        // all linting warnings all the time (see/link references most notably).
        options.addStringOption("Xdoclint:syntax", "-quiet")

        // Suppress warnings due to cross-module @see and @link references.
        // Note that global 'api' task does display all warnings, and
        // checks for 'reference' on top of 'syntax'.
        logging.captureStandardError LogLevel.INFO
        logging.captureStandardOutput LogLevel.INFO  // suppress "## warnings" message

        afterEvaluate {
            configure(options) {
                encoding "UTF-8"
                charSet 'UTF-8'
                author true
                version true
                failOnError false
                links "https://docs.oracle.com/javase/8/docs/api"
            }
        }
    }

    test {
        dependsOn("cleanTest", "generatePomFileForMavenJavaPublication")
        useJUnitPlatform()
        finalizedBy jacocoTestReport
    }

    jacocoTestReport {
        dependsOn test
        reports {
            xml.required = true
            csv.required = false
            // html.outputLocation = layout.buildDirectory.dir('jacocoHtml')
        }
        executionData.from = files("${project.buildDir}/jacoco/test.exec")
    }

    jacoco {
        toolVersion = libs.versions.jacoco.get()
    }

    task javadocJar(type: Jar) {
        archiveClassifier = 'javadoc'
        from javadoc
    }

    tasks.whenTaskAdded { task ->
        if (task.name.contains('signMavenJavaPublication') && project.hasProperty('signing.secretKeyRingFile')) {
            task.enabled = new File(project.property('signing.secretKeyRingFile') as String).isFile()
        }
    }

    publishing {
        if (version.endsWith('SNAPSHOT')) {
            repositories {
                maven {
                    name = "snapshots"
                    url = "https://central.sonatype.com/repository/maven-snapshots/"
                    credentials {
                        if (project.hasProperty('storedUsername') && project.hasProperty('storedPassword')) {
                            username project.storedUsername
                            password project.storedPassword
                        }
                    }
                }
            }
        } else {
            mavenCentral {
                if (project.hasProperty('storedUsername') && project.hasProperty('storedPassword')) {
                    authToken = "${project.storedUsername}:${project.storedPassword}".bytes.encodeBase64().toString()
                }
                // 默认自动发布 AUTOMATIC
                publishingType = 'USER_MANAGED'
                maxWait = 600
            }
        }

        publications {
            mavenJava(MavenPublication) {
                if (!isBomProject) {
                    from components.java
                    artifact sourcesJar
                    artifact javadocJar
                }
                pom {
                    name = artifactId
                    description = 'Provide springboot starter for dify to simplify development'
                    if (isBomProject) {
                        packaging 'pom'
                    } else {
                        packaging 'jar'
                    }
                    url = 'https://github.com/guoshiqiufeng/dify-spring-boot-starter'

                    scm {
                        connection = 'scm:git@github.com:guoshiqiufeng/dify-spring-boot-starter.git'
                        developerConnection = 'scm:git@github.com:guoshiqiufeng/dify-spring-boot-starter.git'
                        url = 'https://github.com/guoshiqiufeng/dify-spring-boot-starter'
                    }

                    licenses {
                        license {
                            name = 'The Apache License, Version 2.0'
                            url = 'https://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }

                    developers {
                        developer {
                            id = 'fubluesky'
                            name = 'yanghq'
                            email = 'fubluesky@foxmail.com'
                        }
                    }

                    withXml {
                        def root = asNode()
                        root.dependencies.'*'.findAll {
                            def d = it
                            d.scope.text() == 'runtime' && project.configurations.findByName("implementation").allDependencies.find { dep ->
                                dep.name == it.artifactId.text()
                            }.each() {
                                d.scope*.value = 'compile'
                                d.appendNode('optional', true)
                            }
                        }
                    }
                }
            }
        }

        signing {
            sign publishing.publications.mavenJava
        }
    }
}


// 提取方法：确定项目对应的Spring Boot版本
static String getSpringBootVersionForProject(Project project) {
    if (project.name.contains("core") || project.name.contains("client")
            || project.name.contains("support") || project.name.contains("status")
            || project.name.contains("spring-boot2-starter")
            || project.name.contains("spring-boot-starter-common") || project.name.contains("java-starter")) {
        return "springBoot2"
    } else if (project.name.contains("spring-boot4-starter")) {
        return "springBoot4"
    }
    return "springBoot3"
}

// 提取方法：配置依赖管理
void configureDependencyManagement(Project project) {
    project.dependencyManagement {
        imports {
            mavenBom "org.springframework.boot:spring-boot-dependencies:${libs.versions[springBootVersionForProject].get()}"
            mavenBom "org.testcontainers:testcontainers-bom:${libs.versions.testcontainers.get()}"
        }
    }
}
